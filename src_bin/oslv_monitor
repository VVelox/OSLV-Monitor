#!perl

=head1 NAME

oslv_monitor - LibreNMS style JSON SNMP extend for OS level virtualization monitoring

=head1 SYNOPSIS

oslv_monitor [B<-c>] [B<-b> <backend>]

=head1 DESCRIPTION

For cron...

    */5 * * * * /usr/local/bin/oslv_monitor -q

For snmpd...

    extend oslv_monitor /bin/cat /var/cache/oslv_monitor/snmp

=head1 FLAGS

=head2 -b <backend>

The backend to use.

Defaults are as below.

    FreeBSD: FreeBSD
    Linux: cgroup

=head2 -c

Compress the output using gzip and base64 encoded so it
can be transmitted via SNMP with out issue.

=cut

use JSON;
use strict;
use warnings;
use Getopt::Long;
use JSON;
use OSLV::Monitor;
use MIME::Base64;
use IO::Compress::Gzip qw(gzip $GzipError);
use File::Slurp;

sub version {
	print "oslv_monitor v. 0.0.1\n";
}

sub help {
	&version;

	print '
-b <backend>    The backend to use.

-c              gzip the json and then base64 encode it

Backends include by default...
FreeBSD - Default on FreeBSD
cgroups - Default on Linux
';
} ## end sub help

my $backend;
if ( $^O eq 'freebsd' ) {
	$backend = 'FreeBSD';
} elsif ( $^O eq 'linux' ) {
	$backend = 'cgroup';
}

# get the commandline options
my $help     = 0;
my $version  = 0;
my $base_dir = '/var/cache/oslv_monitor';
my $quiet;
Getopt::Long::Configure('no_ignore_case');
Getopt::Long::Configure('bundling');
GetOptions(
	'version' => \$version,
	'v'       => \$version,
	'help'    => \$help,
	'h'       => \$help,
	'b=s'     => \$backend,
	'o=s'     => \$base_dir,
	'q'       => \$quiet,
);

my $monitor = OSLV::Monitor->new( backend => $backend, base_dir => $base_dir, );

if ( !-d $base_dir ) {
	mkdir($base_dir) || die( 'Unable to mkdir "' . $base_dir . '"' );
}

eval { $monitor->load; };
if ($@) {
	print encode_json( { version => 1, data => {}, error => 1, errorString => 'load failed... ' . $@ } ) . "\n";
	exit 1;
}

my $data = encode_json( $monitor->run );

if ( !$quiet ) {
	print $data. "\n";
}

write_file( $base_dir . '/json', $data . "\n" );

my $toReturnCompressed;
gzip \$data => \$toReturnCompressed;
my $compressed = encode_base64($toReturnCompressed);
$compressed =~ s/\n//g;
$compressed = $compressed . "\n";

write_file( $base_dir . '/snmp', $compressed );
